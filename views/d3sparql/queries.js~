  var data = [
      {
	"name": "Chart",
        "endpoint": `http://dbpedia.org/sparql`,
	"query": `PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX yago: <http://dbpedia.org/class/yago/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT ?pref ?area
WHERE {
  ?s a yago:PrefecturesOfJapan ;
     rdfs:label ?pref ;
     dbpedia-owl:areaTotal ?area_total .
  FILTER (lang(?pref) = 'en')
  BIND ((?area_total / 1000 / 1000) AS ?area)
}
ORDER BY DESC(?area)
      `
	},
{
        "name": "ScatterPlot",
	"endpoint": `http://togostanza.org/sparql`,
        "query": `prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix up: <http://purl.uniprot.org/core/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix tax: <http://purl.uniprot.org/taxonomy/>
prefix insdc: <http://ddbj.nig.ac.jp/ontologies/sequence#>

select distinct ?length ?genes ?hks ?taxid ?label
where {
  # Cyanobacteria (1117)
  ?taxid rdfs:subClassOf+ <http://identifiers.org/taxonomy/1117> .
  ?taxid rdf:type <http://identifiers.org/taxonomy/> .
  ?s rdfs:seeAlso ?taxid .
  ?s rdfs:label ?label .
  ?s rdf:type obo:SO_0000340 . # chromosome
  ?s insdc:sequence_length ?length .
  {
    select ?s (count(?gene) as ?genes)
    where {
      ?gene obo:so_part_of ?s .
      ?gene rdf:type obo:SO_0000704 . # gene
    }
    group by ?s
  }
  {
    select ?s (count(?cds) as ?hks)
    where {
      # Signal transduction histidine kinase (IPR005467)
      ?cds rdfs:seeAlso <http://identifiers.org/interpro/IPR005467> .
      ?cds obo:so_part_of+ ?s .
    }
    group by ?s
  }
} order by desc(?genes) limit 100
      `
},
{
        "name": "ForceGraph",
        "endpoint": `http://dbpedia.org/sparql`,
        "query": `# https://en.wikipedia.org/wiki/History_of_programming_languages
# https://en.wikipedia.org/wiki/Perl
# http://dbpedia.org/page/Perl
# http://dbpedia.org/sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/resource/>

SELECT DISTINCT ?lang1 ?lang2 ?lang1label ?lang2label ?lang1value ?lang2value ?lang1year ?lang2year
WHERE {
  ?lang1 rdf:type dbpedia-owl:ProgrammingLanguage ;
         rdfs:label ?lang1name ;
         dbpprop:year ?lang1year .
  ?lang2 rdf:type dbpedia-owl:ProgrammingLanguage ;
         rdfs:label ?lang2name ;
         dbpprop:year ?lang2year .
  ?lang1 dbpedia-owl:influenced ?lang2 .
  FILTER (?lang1 != ?lang2)
  FILTER (LANG(?lang1name) = 'en')
  FILTER (LANG(?lang2name) = 'en')
  BIND (replace(?lang1name, " .programming language.", "") AS ?lang1label)
  BIND (replace(?lang2name, " .programming language.", "") AS ?lang2label)
  FILTER (?lang1year > 1950 AND ?lang1year < 2020)
  FILTER (?lang2year > 1950 AND ?lang2year < 2020)
  # To render older language larger than newer
  BIND ((2020 - ?lang1year) AS ?lang1value)
  BIND ((2020 - ?lang2year) AS ?lang2value)
}
      `
},
{
        "name": "Sankey",
        "endpoint": `http://dbpedia.org/sparql`,
        "query": `PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/resource/>

SELECT DISTINCT ?parent ?parent_name ?child ?child_name
WHERE {
  VALUES ?root { dbpedia:Fortran }
  ?root   rdf:type dbpedia-owl:ProgrammingLanguage ;
          rdfs:label ?root_label .
  ?parent rdf:type dbpedia-owl:ProgrammingLanguage ;
          rdfs:label ?parent_label ;
          dbpprop:year ?parent_year .
  ?child  rdf:type dbpedia-owl:ProgrammingLanguage ;
          rdfs:label ?child_label ;
          dbpprop:year ?child_year .
  ?root   dbpedia-owl:influenced* ?child .
  ?parent dbpedia-owl:influenced ?child .
  BIND (xsd:integer(?parent_year) AS ?pyear)
  FILTER (?pyear > 1950 and ?pyear < 2020)
  BIND (xsd:integer(?child_year) AS ?cyear)
  FILTER (?cyear > 1950 and ?cyear < 2020)
  FILTER (?pyear < ?cyear)
  FILTER (?root != ?child)
  FILTER (?parent != ?child)
  FILTER (LANG(?root_label) = 'en')
  FILTER (LANG(?parent_label) = 'en')
  FILTER (LANG(?child_label) = 'en')
  BIND (replace(?parent_label, " .programming language.", "") AS ?parent_name)
  BIND (replace(?child_label, " .programming language.", "") AS ?child_name)
}
      `
},
{
        "name": "RoundTree",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT ?root_name ?parent_name ?child_name
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
  VALUES ?root_name { "Hypsibiidae" }
  ?root up:scientificName ?root_name .
  ?child rdfs:subClassOf+ ?root .
  ?child rdfs:subClassOf ?parent .
  ?child up:scientificName ?child_name .
  ?parent up:scientificName ?parent_name .
}
      `
},
{
        "name": "Dendogram",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `PREFIX up: <http://purl.uniprot.org/core/>
PREFIX tax: <http://purl.uniprot.org/taxonomy/>
SELECT ?root_name ?parent_name ?child_name
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
  VALUES ?root_name { "Tardigrada" }
  ?root up:scientificName ?root_name .
  ?child rdfs:subClassOf+ ?root .
  ?child rdfs:subClassOf ?parent .
  ?child up:scientificName ?child_name .
  ?parent up:scientificName ?parent_name .
}
      `
},
{
        "name": "TreeMap",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT ?root_name ?parent_name ?child_name
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
  VALUES ?root_name { "Tardigrada" }
  ?root up:scientificName ?root_name .
  ?child rdfs:subClassOf+ ?root .
  ?child rdfs:subClassOf ?parent .
  ?child up:scientificName ?child_name .
  ?parent up:scientificName ?parent_name .
}
      `
},
{
        "name": "TreeMapZoom",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `# This may take a while to get results
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX ec: <http://purl.uniprot.org/enzyme/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT  (REPLACE(STR(?root), ".*/", "") AS ?root_label)
        (REPLACE(STR(?parent), ".*/", "") AS ?parent_label)
        (REPLACE(STR(?enzyme), ".*/", "") AS ?enzyme_label)
        (COUNT(?protein) AS ?value)
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
        VALUES ?root { ec:1.-.-.- }
        ?root skos:narrowerTransitive* ?enzyme .
        ?parent skos:narrowerTransitive ?enzyme .
        ?protein up:enzyme ?enzyme .
}
GROUP BY ?root ?parent ?enzyme ORDER BY ?enzyme
      `
},
{
        "name": "Sunburst",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT ?root_name ?parent_name ?child_name
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
  VALUES ?root_name {"Tardigrada"} .
  ?root up:scientificName ?root_name .
  ?child rdfs:subClassOf+ ?root .
  ?child rdfs:subClassOf ?parent .
  ?child up:scientificName ?child_name .
  ?parent up:scientificName ?parent_name .
}
      `
},
{
        "name": "CirclePack",
        "endpoint": `http://togostanza.org/sparql`,
        "query": `PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT ?root_name ?parent_name ?child_name
FROM <http://togogenome.org/graph/uniprot/>
WHERE
{
  VALUES ?root_name { "Tardigrada" }
  ?root up:scientificName ?root_name .
  ?child rdfs:subClassOf+ ?root .
  ?child rdfs:subClassOf ?parent .
  ?child up:scientificName ?child_name .
  ?parent up:scientificName ?parent_name .
}
      `
},
 {
	"name": "openstreetmap",
        "endpoint": `http://www.ebi.ac.uk/rdf/services/biosamples/sparql`,
	"query": `
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX efo: <http://www.ebi.ac.uk/efo/>
PREFIX biosd-terms: <http://rdf.ebi.ac.uk/terms/biosd/>
PREFIX pav: <http://purl.org/pav/2.0/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX sio: <http://semanticscience.org/resource/>

# Samples reporting latitude and longitude
SELECT DISTINCT ?item ?lat ?lng
WHERE {
  ?item biosd-terms:has-sample-attribute ?lat_value, ?lng_value .

  ?lat_value
    dc:type ?lat_label;
    sio:SIO_000300 ?lat . # sio:has value

  FILTER ( LCASE ( STR ( ?lat_label ) ) = "latitude" ) .

  ?lng_value
    dc:type ?lng_label;
    sio:SIO_000300 ?lng . # sio:has value

  FILTER ( LCASE ( STR ( ?lng_label ) ) = "longitude" ) .
} LIMIT 1000
      
`
	}

    ]
